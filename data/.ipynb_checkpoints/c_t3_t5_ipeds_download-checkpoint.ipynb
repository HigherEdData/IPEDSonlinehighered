{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "f08ffaa4-6426-4769-8f95-3bd3650e6d01",
   "metadata": {},
   "source": [
    "## Promising or Predatory? Online Education in Non-Profit and For-Profit Universities\n",
    "### By Christian Michael Smith, Amber D. Villalobos (equal authors), Laura T. Hamilton, and Charlie Eaton\n",
    "\n",
    "### Code by Christian Smith"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f7d57f19-61af-4592-a796-b7f3b0f1fad8",
   "metadata": {},
   "source": [
    "### (1) Download and Clean Data for Tables 3 and 5"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "22321fe8-1ba4-4eae-8df1-728da430c6b9",
   "metadata": {},
   "outputs": [],
   "source": [
    "*Prepare workspace for new code\n",
    "cap log close\n",
    "clear all\n",
    "set more off\n",
    "set matsize 11000"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3004084b-15f5-4883-8b6b-e965a1077f2b",
   "metadata": {},
   "outputs": [],
   "source": [
    "**********(A) PREPARE EACH INDIVIDUAL DATASET**********\n",
    "\n",
    "\n",
    "\n",
    "***(I) PREPARE DATA ON ONLINE PREVALENCE***\n",
    "\n",
    "*2012-2018\n",
    "qui {\n",
    "forvalues year=2012/2018 {  \n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/EF`year'A_DIST_Data_Stata.zip ///\n",
    "    EF`year'A_DIST_Data_Stata.zip, replace\n",
    "    }\n",
    "}\n",
    "\n",
    "qui { \n",
    "forvalues year=2012/2018 {\n",
    "    unzipfile EF`year'A_DIST_Data_Stata, replace\n",
    "\tquietly insheet using ef`year'a_dist_rv_data_stata.csv, clear\n",
    "    rm ef`year'a_dist_rv_data_stata.csv\n",
    "    rm ef`year'a_dist_data_stata.csv\n",
    "    drop x*\n",
    "    keep if efdelev==2\n",
    "    gen year=`year'\n",
    "    save ef`year'a_dist, replace\n",
    "\t\t}\n",
    "\t}\n",
    "\n",
    "*2019-2020 (coded slightly differently in IPEDS)\n",
    "qui {\n",
    "forvalues year=2019/2020 {  \n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/EF`year'A_DIST_Data_Stata.zip EF`year'A_DIST_Data_Stata.zip, replace\n",
    "\tunzipfile EF`year'A_DIST_Data_Stata, replace\n",
    "\tquietly insheet using ef`year'a_dist_data_stata.csv, clear\n",
    "    rm ef`year'a_dist_data_stata.csv\n",
    "    drop x*\n",
    "    keep if efdelev==2\n",
    "    gen year=`year'\n",
    "    save ef`year'a_dist, replace\n",
    "\t\t}\n",
    "    rm ef2019a_dist_rv_data_stata.csv\n",
    "\t}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a6226696-592d-4b9b-835d-6c80e5c7c4a4",
   "metadata": {},
   "outputs": [],
   "source": [
    "***(II) PREPARE DATA ON INSTITUTIONAL CHARACTERISTICS***\n",
    "\n",
    "qui {\n",
    "forvalues year=2012/2020 {\n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/HD`year'_Data_Stata.zip HD`year'_Data_Stata.zip, replace\n",
    "\tunzipfile HD`year'_Data_Stata, replace\n",
    "\tinsheet using hd`year'_data_stata.csv, clear\n",
    "    rm hd`year'_data_stata.csv\n",
    "    tostring ein gentele opeid zip closedat, replace force\n",
    "    save hd`year'a, replace\n",
    "\t\t}   \n",
    "\t}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9d746d63-46c1-420d-b31b-f42279c16eb8",
   "metadata": {},
   "outputs": [],
   "source": [
    "***(III) PREPARE DATA ON ENROLLMENT COUNTS***\n",
    "\n",
    "*2012-2018\n",
    "\t*Total count of undergraduates (by race)\n",
    "qui {\n",
    "forvalues year=2012/2018 {\n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/EF`year'A_Data_Stata.zip EF`year'A_Data_Stata.zip, replace\n",
    "\tunzipfile EF`year'A_Data_Stata, replace\n",
    "\tinsheet using ef`year'a_rv_data_stata.csv, clear\n",
    "\tkeep if efalevel==2\n",
    "\tforeach var in efbkaat efaiant efasiat efhispt efwhitt eftotlm eftotlw eftotlt {\n",
    "        rename `var' u`var'\n",
    "\t\t\t}\n",
    "\tgen year=`year'\n",
    "\tsave under`year', replace\n",
    "\t*Count of full-time, first-time, degree/cert.-seeking (\"traditional\") undergraduates (by race)\n",
    "\tinsheet using ef`year'a_rv_data_stata.csv, clear\n",
    "\tkeep if efalevel==24\n",
    "\tforeach var in efbkaat efaiant efasiat efhispt efwhitt eftotlm eftotlw eftotlt {\n",
    "        rename `var' t`var'\n",
    "\t\t\t}\n",
    "    gen year=`year'\n",
    "\tsave trad`year', replace\n",
    "\t*Count of first-time, degree/cert.-seeking undergraduates (by race)\n",
    "\tinsheet using ef`year'a_rv_data_stata.csv, clear\n",
    "\tkeep if efalevel==4\n",
    "\tforeach var in efbkaat efaiant efasiat efhispt efwhitt eftotlm eftotlw eftotlt {\n",
    "        rename `var' f`var'\n",
    "\t\t\t}\n",
    "    gen year=`year'\n",
    "\tsave first`year', replace\n",
    "\trm ef`year'a_rv_data_stata.csv\n",
    "\trm ef`year'a_data_stata.csv\n",
    "\t\t}\n",
    "\t}\n",
    "\n",
    "*2019-2020 (coded slightly differently in IPEDS)\n",
    "qui {\n",
    "forvalues year=2019/2020 {\n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/EF`year'A_Data_Stata.zip EF`year'A_Data_Stata.zip, replace\n",
    "\tunzipfile EF`year'A_Data_Stata, replace\n",
    "\tinsheet using ef`year'a_data_stata.csv, clear\n",
    "\tkeep if efalevel==2\n",
    "\tforeach var in efbkaat efaiant efasiat efhispt efwhitt eftotlm eftotlw eftotlt {\n",
    "        rename `var' u`var'\n",
    "\t\t\t}\n",
    "\tgen year=`year'\n",
    "\tsave under`year', replace\n",
    "\t*Count of full-time, first-time, degree/cert.-seeking (\"traditional\") undergraduates (by race)\n",
    "\tinsheet using ef`year'a_data_stata.csv, clear\n",
    "\tkeep if efalevel==24\n",
    "\tforeach var in efbkaat efaiant efasiat efhispt efwhitt eftotlm eftotlw eftotlt {\n",
    "        rename `var' t`var'\n",
    "\t\t\t}\n",
    "\tgen year=`year'\n",
    "\tsave trad`year', replace\n",
    "\t*Count of first-time, degree/cert.-seeking undergraduates (by race)\n",
    "\tinsheet using ef`year'a_data_stata.csv, clear\n",
    "\tkeep if efalevel==4\n",
    "\tforeach var in efbkaat efaiant efasiat efhispt efwhitt eftotlm eftotlw eftotlt {\n",
    "        rename `var' f`var'\n",
    "\t\t\t}\n",
    "\tgen year=`year'\n",
    "\tsave first`year', replace\n",
    "    rm ef`year'a_data_stata.csv\n",
    "\t\t}\n",
    "    rm ef2019a_rv_data_stata.csv\n",
    "\t}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "19ca6867-991c-40b0-ab71-c9801a525d97",
   "metadata": {},
   "outputs": [],
   "source": [
    "***(IV) PREPARE DATA ON TOTAL FTES INCLUDING GRAD STUDENTS***\n",
    "\n",
    "*2012-2018\n",
    "qui {\n",
    "forvalues year=2012/2018 {\n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/EFIA`year'_Data_Stata.zip EFIA`year'_Data_Stata.zip, replace\n",
    "\tunzipfile EFIA`year'_Data_Stata, replace\n",
    "\tinsheet using efia`year'_rv_data_stata.csv, clear\n",
    "\treplace eftegd=0 if eftegd==. & efteug!=. \n",
    "\treplace efteug=0 if efteug==. & eftegd!=. \n",
    "\tgen totalfte = eftegd+efteug\n",
    "\treplace totalfte=. if totalfte==0\n",
    "\tgen year=`year'\n",
    "\tsave efia`year', replace\n",
    "    rm efia`year'_rv_data_stata.csv\n",
    "    rm efia`year'_data_stata.csv\n",
    "\t\t}\n",
    "*2019 (coded slightly differently in IPEDS)\n",
    "forvalues year=2019/2019 {\n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/EFIA`year'_Data_Stata.zip EFIA`year'_Data_Stata.zip, replace\n",
    "\tunzipfile EFIA`year'_Data_Stata, replace\n",
    "\tinsheet using efia`year'_data_stata.csv, clear\n",
    "\treplace eftegd=0 if eftegd==. & efteug!=.\n",
    "\treplace efteug=0 if efteug==. & eftegd!=.\n",
    "\tgen totalfte = eftegd+efteug\n",
    "\treplace totalfte=. if totalfte==0\n",
    "\tgen year=`year'\n",
    "\tsave efia`year', replace\n",
    "    rm efia`year'_rv_data_stata.csv\n",
    "    rm efia`year'_data_stata.csv\n",
    "\t\t}\n",
    "\t}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "71f1fc00-b686-47a8-9f16-8d24cd042de3",
   "metadata": {},
   "outputs": [],
   "source": [
    "***(V) PREPARE DATA ON RETENTION RATES***\n",
    "\n",
    "*2012-2017 entering cohorts\n",
    "qui {\n",
    "forvalues year=2013/2018 {  \n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/EF`year'D_Data_Stata.zip EF`year'D_Data_Stata.zip, replace\n",
    "\tunzipfile EF`year'D_Data_Stata, replace\n",
    "\tquietly insheet using ef`year'd_rv_data_stata.csv, clear\n",
    "    drop x*\n",
    "    gen year=`year'-1\n",
    "\tlocal yyyy=`year'-1\n",
    "\tdrop stufacr\n",
    "    save ret`yyyy', replace\n",
    "    rm ef`year'd_rv_data_stata.csv\n",
    "    rm ef`year'd_data_stata.csv\n",
    "\t\t}\n",
    "\t}\n",
    "\n",
    "*2018-2019 entering cohorts (coded slightly differently in IPEDS)\n",
    "qui {\n",
    "forvalues year=2019/2020 {  \n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/EF`year'D_Data_Stata.zip EF`year'D_Data_Stata.zip, replace\n",
    "\tunzipfile EF`year'D_Data_Stata, replace\n",
    "\tquietly insheet using ef`year'd_data_stata.csv, clear\n",
    "    drop x*\n",
    "    gen year=`year'-1\n",
    "\tlocal yyyy=`year'-1\n",
    "    drop stufacr\n",
    "\tsave ret`yyyy', replace\n",
    "    rm ef`year'd_data_stata.csv\n",
    "\t\t}\n",
    "    rm ef2019d_rv_data_stata.csv\n",
    "\t}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d7d8ac30-258e-4775-b308-52b36fc29235",
   "metadata": {},
   "outputs": [],
   "source": [
    "***(VI) PREPARE DATA ON GRADUATION RATES***\n",
    "\n",
    "*2012 fall entering cohort\n",
    "qui {\n",
    "forvalues year=2018/2018 {  \n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/GR`year'_Data_Stata.zip GR`year'_Data_Stata.zip, replace\n",
    "\tunzipfile GR`year'_Data_Stata, replace\n",
    "\tquietly insheet using gr`year'_rv_data_stata.csv, clear\n",
    "    drop x*\n",
    "    gen year=`year'-6\n",
    "\tlocal yyyy=`year'-6\n",
    "\tgen gradnum6 = .\n",
    "\treplace gradnum6 = grtotlt if grtype==3\n",
    "\tgen cohortnum = .\n",
    "\treplace cohortnum = grtotlt if grtype==2\n",
    "\tsort unitid gradnum6\n",
    "\tbysort unitid: replace gradnum6=gradnum6[1]\n",
    "\tsort unitid cohortnum\n",
    "\tbysort unitid: replace cohortnum=cohortnum[1]\n",
    "\tduplicates drop unitid, force\n",
    "\tgen gradrate6 = 100*gradnum6/cohortnum\n",
    "\tkeep unitid year gradrate6 gradnum6 cohortnum\n",
    "    save gr`yyyy', replace\n",
    "    rm gr`year'_rv_data_stata.csv\n",
    "    rm gr`year'_data_stata.csv\n",
    "\t\t}\n",
    "\t}\n",
    "\n",
    "    *2013-2014 fall entering cohorts (coded slightly differently in IPEDS)\n",
    "qui {\n",
    "forvalues year=2019/2020 {  \n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/GR`year'_Data_Stata.zip GR`year'_Data_Stata.zip, replace\n",
    "\tunzipfile GR`year'_Data_Stata, replace\n",
    "\tquietly insheet using gr`year'_data_stata.csv, clear\n",
    "    drop x*\n",
    "    gen year=`year'-6\n",
    "\tlocal yyyy=`year'-6\n",
    "\tgen gradnum6 = .\n",
    "\treplace gradnum6 = grtotlt if grtype==3\n",
    "\tgen cohortnum = .\n",
    "\treplace cohortnum = grtotlt if grtype==2\n",
    "\tsort unitid gradnum6\n",
    "\tbysort unitid: replace gradnum6=gradnum6[1]\n",
    "\tsort unitid cohortnum\n",
    "\tbysort unitid: replace cohortnum=cohortnum[1]\n",
    "\tduplicates drop unitid, force\n",
    "\tgen gradrate6 = 100*gradnum6/cohortnum\n",
    "\tkeep unitid year gradrate6 gradnum6 cohortnum\n",
    "\tsave gr`yyyy', replace\n",
    "    rm gr`year'_data_stata.csv\n",
    "\t\t}\n",
    "    rm gr2019_rv_data_stata.csv\n",
    "\t}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "28e23cc7-af8e-4b7b-9e53-0ff672c37db1",
   "metadata": {},
   "outputs": [],
   "source": [
    "***(VII) PREPARE DATA ON FINANCIAL AID***\n",
    "\n",
    "*2012-2017\n",
    "qui {\n",
    "forvalues year=12/17 {\n",
    "    local yy = `year'+1\n",
    "    local yyyy = 2000+`yy'\n",
    "\tlocal yyyy_fall = 2000+`yy'-1\n",
    "    copy https://nces.ed.gov/ipeds/datacenter/data/SFA`year'`yy'_Data_Stata.zip SFA`year'`yy'_Data_Stata.zip, replace\n",
    "    unzipfile SFA`year'`yy'_Data_Stata, replace\n",
    "    insheet using sfa`year'`yy'_rv_data_stata.csv, clear\n",
    "    gen year=`yyyy_fall'\n",
    "    drop x*\n",
    "    save sfa`yyyy_fall', replace\n",
    "    rm sfa`year'`yy'_data_stata.csv\n",
    "    rm sfa`year'`yy'_rv_data_stata.csv\n",
    "    }\n",
    "}\n",
    "\n",
    "*2018-2019 (coded slightly differently in IPEDS)\n",
    "qui {\n",
    "forvalues year=18/19 {\n",
    "\tlocal yy = `year'+1\n",
    "\tlocal yyyy = 2000+`yy'\n",
    "\tlocal yyyy_fall = 2000+`yy'-1\n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/SFA`year'`yy'_Data_Stata.zip SFA`year'`yy'_Data_Stata.zip, replace\n",
    "\tunzipfile SFA`year'`yy'_Data_Stata, replace\n",
    "\tinsheet using sfa`year'`yy'_data_stata.csv, clear\n",
    "\tgen year=`yyyy_fall'\n",
    "\tdrop x*\n",
    "\tsave sfa`yyyy_fall', replace\n",
    "    rm sfa`year'`yy'_data_stata.csv\n",
    "\t\t}\n",
    "    rm sfa1819_rv_data_stata.csv\n",
    "\t}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "82b6fc1f-41d3-4a9e-b085-d1029de3b621",
   "metadata": {},
   "outputs": [],
   "source": [
    "***(VIII) PREPARE DATA ON ACCEPTANCE RATES***\n",
    "\n",
    "*2012-2013\n",
    "qui {\n",
    "forvalues year=2012/2013 {  \n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/IC`year'_Data_Stata.zip IC`year'_Data_Stata.zip, replace\n",
    "\tunzipfile IC`year'_Data_Stata, replace\n",
    "\tquietly insheet using ic`year'_rv_data_stata.csv, clear\n",
    "\tdrop x*\n",
    "\tgen year=`year'\n",
    "\tsave adm`year', replace\n",
    "    rm ic`year'_rv_data_stata.csv\n",
    "    rm ic`year'_data_stata.csv\n",
    "\t\t}\n",
    "\t}\n",
    "\n",
    "*2014-2018 (coded slightly differently in IPEDS)\n",
    "qui {\n",
    "forvalues year=2014/2018 {  \n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/ADM`year'_Data_Stata.zip ADM`year'_Data_Stata.zip, replace\n",
    "\tunzipfile ADM`year'_Data_Stata, replace\n",
    "\tquietly insheet using adm`year'_rv_data_stata.csv, clear\n",
    "\tdrop x*\n",
    "\tgen year=`year'\n",
    "\tsave adm`year', replace\n",
    "    rm adm`year'_rv_data_stata.csv\n",
    "    rm adm`year'_data_stata.csv\n",
    "\t\t}\n",
    "\t}\n",
    "\n",
    "*2019-2020 (coded slightly differently in IPEDS)\n",
    "qui {\n",
    "forvalues year=2019/2020 {  \n",
    "\tcopy https://nces.ed.gov/ipeds/datacenter/data/ADM`year'_Data_Stata.zip ADM`year'_Data_Stata.zip, replace\n",
    "\tunzipfile ADM`year'_Data_Stata, replace\n",
    "\tquietly insheet using adm`year'_data_stata.csv, clear\n",
    "\tdrop x*\n",
    "\tgen year=`year'\n",
    "\tsave adm`year', replace\n",
    "    rm adm`year'_data_stata.csv\n",
    "\t\t}\n",
    "    rm adm2019_rv_data_stata.csv\n",
    "\t}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "cf31ac97-fc7d-4217-a41e-aadcaafc475e",
   "metadata": {},
   "outputs": [],
   "source": [
    "***(IX) PREPARE DATA ON LOAN PROGRESS OUTCOMES FROM SCORECARD***\n",
    "\n",
    "qui {\n",
    "clear\n",
    "insheet using MERGED2018_19_PP.csv\n",
    "keep unitid bbrr* dbrr*\n",
    "gen year=2013\n",
    "save repay2013.dta, replace\n",
    "\t}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "cc43c712-3a4e-47dd-b395-382ae7680c0d",
   "metadata": {},
   "outputs": [],
   "source": [
    "**********(B) PERFORM FINAL MERGE**********\t\n",
    "\n",
    "qui {\n",
    "use hd2012a, clear\n",
    "gen year=2012\n",
    "forvalues year=2013/2020 {\n",
    "\tappend using hd`year'a\n",
    "    replace year=`year' if year==.\n",
    "\t\t}\n",
    "\n",
    "forvalues year=2012/2020 {\n",
    "    merge 1:1 unitid year using ef`year'a_dist, nogen update\n",
    "    merge 1:1 unitid year using first`year', nogen keepusing(f*) update\n",
    "    merge 1:1 unitid year using under`year', nogen keepusing(u*) update\n",
    "\tmerge 1:1 unitid year using trad`year', nogen keepusing(t*) update\n",
    "\tmerge 1:1 unitid year using adm`year', nogen update\n",
    "\t\t}\n",
    "\n",
    "forvalues year=2012/2019 {\n",
    "    merge 1:1 unitid year using efia`year',  nogen update\n",
    "\tmerge 1:1 unitid year using sfa`year', nogen update\n",
    "\tmerge 1:1 unitid year using ret`year', nogen update\n",
    "\t\t}\n",
    "\n",
    "forvalues year=2012/2014 {\n",
    "\tmerge 1:1 unitid year using gr`year', nogen update\n",
    "\t\t}\n",
    "\n",
    "merge 1:1 unitid year using repay2013, nogen update\n",
    "\n",
    "\t}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "24e2b2e5-17ab-4706-bdfc-50ec89a84c63",
   "metadata": {},
   "outputs": [],
   "source": [
    "**********(C) CREATE / CLEAN VARIABLES AND SAVE ANALYTIC DATASET**********\n",
    "\n",
    "*Miscellaneous data-cleaning and variable construction\n",
    "qui {\n",
    "gen forprofit = (control==3)\n",
    "gen nonprofit = (control!=3)\n",
    "gen allcontrols = 1\n",
    "foreach var in forprofit nonprofit {\n",
    "\treplace `var' = . if control==.\n",
    "\t\t}\n",
    "label define forprofit_lab 0 \"Nonprofit (public or private)\" 1 \"For-profit\"\n",
    "label values forprofit forprofit_lab\n",
    "label define nonprofit_lab 0 \"For-profit\" 1 \"Nonprofit (public or private)\" \n",
    "label values nonprofit nonprofit_lab\n",
    "gen blackpct = fefbkaat / feftotlt * 100\n",
    "gen latinxpct = fefhispt / feftotlt * 100\n",
    "gen aianpct = fefaiant / feftotlt * 100\n",
    "gen asiapct = fefasiat / feftotlt * 100\n",
    "gen blackpct_u = uefbkaat / ueftotlt * 100\n",
    "gen latinxpct_u = uefhispt / ueftotlt * 100\n",
    "gen aianpct_u = uefaiant / ueftotlt * 100\n",
    "gen asiapct_u = uefasiat / ueftotlt * 100\n",
    "gen malepct_u = ueftotlm / ueftotlt * 100\n",
    "gen blackpct_t = tefbkaat / teftotlt * 100\n",
    "gen latinxpct_t = tefhispt / teftotlt * 100\n",
    "gen aianpct_t = tefaiant / teftotlt * 100\n",
    "gen asiapct_t = tefasiat / teftotlt * 100\n",
    "gen malepct_t = teftotlm / teftotlt * 100\n",
    "replace hbcu = (hbcu==1)\n",
    "replace locale = . if locale==-3\n",
    "replace hdegofr1  = . if hdegofr1 ==-3\n",
    "sort unitid year\n",
    "gen pctonline = efdeexc / efdetot * 100\n",
    "gen pctonline10 = pctonline/10\n",
    "gen logenrl = ln(ueftotlt)\n",
    "gen acceptrate = 100*admssn/applcn\n",
    "replace acceptrate = 100 if acceptrate==.\n",
    "gen totloan_a = floan_a+oloan_a\n",
    "foreach var in bbrr2_fed_ug_dflt bbrr2_fed_ug_dlnq bbrr2_fed_ug_fbr bbrr2_fed_ug_dfr bbrr2_fed_ug_noprog bbrr2_fed_ug_makeprog bbrr2_fed_ug_paidinfull bbrr2_fed_ug_discharge {\n",
    "\treplace `var' = \"\" if `var'==\"NULL\"\n",
    "\treplace `var' = \"\" if `var'==\"PrivacySuppressed\"\n",
    "\tdestring `var', replace\n",
    "\treplace `var' = 100*`var'\n",
    "\t\t}\n",
    "\t}\n",
    "\n",
    "*Save final analytic dataset for Table 3 and Table 5\n",
    "save d_t3_t5_ipeds_scorecard, replace"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "769b0533-33c2-4f19-bf69-6aa84bf3a4d9",
   "metadata": {},
   "source": [
    "### (2) Download and Clean Data for Figure 1"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "106cb3b1-d33e-4085-a551-3f5b2450faa8",
   "metadata": {},
   "outputs": [],
   "source": [
    "**********(A) PREPARE EACH INDIVIDUAL DATASET**********\n",
    "\n",
    "***(I) PREPARE DATA ON UNDERGRADUATE ONLINE ENROLLMENTS***\n",
    "\n",
    "*Copy data on online enrollments\n",
    "qui {\n",
    "clear\n",
    "forvalues year=2012/2019 {\n",
    "    copy https://nces.ed.gov/ipeds/datacenter/data/EF`year'A_DIST_Data_Stata.zip EF`year'A_DIST_Data_Stata.zip, replace\n",
    "    }\n",
    "}\n",
    "\n",
    "*2012-2018\n",
    "qui {\n",
    "clear\n",
    "gen temp=.\n",
    "save ef20122019a_dist, replace\n",
    "forvalues year=2012/2018 {\n",
    "    unzipfile EF`year'A_DIST_Data_Stata, replace\n",
    "    quietly insheet using ef`year'a_dist_rv_data_stata.csv, clear\n",
    "    drop x*\n",
    "    keep if efdelev==2\n",
    "    gen year=`year'\n",
    "    append using ef20122019a_dist\n",
    "    save ef20122019a_dist, replace\n",
    "    rm ef`year'a_dist_rv_data_stata.csv\n",
    "    rm ef`year'a_dist_data_stata.csv\n",
    "    }\n",
    "\n",
    "*2019 (coded slightly differently in IPEDS)\n",
    "forvalues year=2019/2019 {   \n",
    "    unzipfile EF`year'A_DIST_Data_Stata, replace\n",
    "    quietly insheet using ef`year'a_dist_data_stata.csv, clear \n",
    "    drop x*\n",
    "    keep if efdelev==2\n",
    "    gen year=`year'\n",
    "    append using ef20122019a_dist\n",
    "    rename efdetot efdetotu\n",
    "    rename efdeexc efdeexcu\n",
    "    keep unitid year efdetotu efdeexcu\n",
    "    save ef20122019a_distu, replace\n",
    "    rm ef`year'a_dist_data_stata.csv\n",
    "    }\n",
    "rm ef20122019a_dist.dta\n",
    "}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "aa87e833-3b23-4bb1-882c-3411c61ee18c",
   "metadata": {},
   "outputs": [],
   "source": [
    "***(II) PREPARE DATA ON UNDERGRADUATE INSTITUTIONAL CHARACTERISTICS***\n",
    "\n",
    "*Copy data on institutional characteristics\n",
    "qui {\n",
    "clear\n",
    "forvalues year=2012/2019 {\n",
    "    copy https://nces.ed.gov/ipeds/datacenter/data/HD`year'_Data_Stata.zip HD`year'_Data_Stata.zip, replace\n",
    "    }\n",
    "}\n",
    "\n",
    "*Prepare data on institutional characteristics\n",
    "qui {\n",
    "clear\n",
    "gen temp=.\n",
    "save hd20122019, replace\n",
    "forvalues minus =0/7 {\n",
    "    local year=2019-`minus'\n",
    "    unzipfile HD`year'_Data_Stata, replace\n",
    "    insheet using hd`year'_data_stata.csv, clear\n",
    "    gen year=`year'\n",
    "    tostring ein opeid zip closedat, replace force\n",
    "    append using hd20122019\n",
    "    tostring duns, replace force\n",
    "    save hd20122019, replace\n",
    "    rm hd`year'_data_stata.csv\n",
    "    }\n",
    "}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8de94c30-1883-4ef3-8ff7-f8c38a62f5c2",
   "metadata": {},
   "outputs": [],
   "source": [
    "**********(B) PERFORM FINAL MERGE**********\n",
    "\n",
    "use hd20122019, clear\n",
    "merge 1:1 unitid year using ef20122019a_distu, nogen\n",
    "rm hd20122019.dta\n",
    "rm ef20122019a_distu.dta"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f990ea55-5403-448c-b49f-89c8cf645a7b",
   "metadata": {},
   "outputs": [],
   "source": [
    "**********(C) CLEAN VARIABLES AND SAVE ANALYTIC DATASET**********\n",
    "\n",
    "*Label variables\n",
    "qui {\n",
    "label variable efdetotu  \"All students enrolled - undergrad\"\n",
    "label variable efdeexcu  \"Students enrolled exclusively online - undergrad\"\n",
    "label variable unitid   \"Unique identification number of the institution\"\n",
    "label variable instnm   \"Institution (entity) name\"\n",
    "label variable ialias   \"Institution name alias\"\n",
    "label variable addr     \"Street address or post office box\"\n",
    "label variable city     \"City location of institution\"\n",
    "label variable stabbr   \"State abbreviation\"\n",
    "label variable zip      \"ZIP code\"\n",
    "label variable fips     \"FIPS state code\"\n",
    "label variable obereg   \"Bureau of Economic Analysis (BEA) regions\"\n",
    "label variable chfnm    \"Name of chief administrator\"\n",
    "label variable chftitle \"Title of chief administrator\"\n",
    "label variable gentele  \"General information telephone number\"\n",
    "label variable ein      \"Employer Identification Number\"\n",
    "label variable duns     \"Dun and Bradstreet numbers\"\n",
    "label variable opeid    \"Office of Postsecondary Education (OPE) ID Number\"\n",
    "label variable opeflag  \"OPE Title IV eligibility indicator code\"\n",
    "label variable webaddr  \"Institution's internet website address\"\n",
    "label variable adminurl \"Admissions office web address\"\n",
    "label variable faidurl  \"Financial aid office web address\"\n",
    "label variable applurl  \"Online application web address\"\n",
    "label variable npricurl \"Net price calculator web address\"\n",
    "label variable veturl   \"Veterans and Military Servicemembers tuition policies web address\"\n",
    "label variable athurl   \"Student-Right-to-Know student athlete graduation rate web address\"\n",
    "label variable disaurl  \"Disability Services Web Address\"\n",
    "label variable sector   \"Sector of institution\"\n",
    "label variable iclevel  \"Level of institution\"\n",
    "label variable control  \"Control of institution\"\n",
    "label variable hloffer  \"Highest level of offering\"\n",
    "label variable ugoffer  \"Undergraduate offering\"\n",
    "label variable groffer  \"Graduate offering\"\n",
    "label variable hdegofr1 \"Highest degree offered\"\n",
    "label variable deggrant \"Degree-granting status\"\n",
    "label variable hbcu     \"Historically Black College or University\"\n",
    "label variable hospital \"Institution has hospital\"\n",
    "label variable medical  \"Institution grants a medical degree\"\n",
    "label variable tribal   \"Tribal college\"\n",
    "label variable locale   \"Degree of urbanization (Urban-centric locale)\"\n",
    "label variable openpubl \"Institution open to the general public\"\n",
    "label variable act      \"Status of institution\"\n",
    "label variable newid    \"UNITID for merged schools\"\n",
    "label variable deathyr  \"Year institution was deleted from IPEDS\"\n",
    "label variable closedat \"Date institution closed\"\n",
    "label variable cyactive \"Institution is active in current year\"\n",
    "label variable postsec  \"Primarily postsecondary indicator\"\n",
    "label variable pseflag  \"Postsecondary institution indicator\"\n",
    "label variable pset4flg \"Postsecondary and Title IV institution indicator\"\n",
    "label variable rptmth   \"Reporting method for student charges, graduation rates, retention rates and student financial aid\"\n",
    "label variable instcat  \"Institutional category\"\n",
    "label variable c18basic \"Carnegie Classification 2018: Basic\"\n",
    "label variable c18ipug  \"Carnegie Classification 2018: Undergraduate Instructional Program\"\n",
    "label variable c18ipgrd \"Carnegie Classification 2018: Graduate Instructional Program\"\n",
    "label variable c18ugprf \"Carnegie Classification 2018: Undergraduate Profile\"\n",
    "label variable c18enprf \"Carnegie Classification 2018: Enrollment Profile\"\n",
    "label variable c18szset \"Carnegie Classification 2018: Size and Setting\"\n",
    "label variable c15basic \"Carnegie Classification 2015: Basic\"\n",
    "label variable ccbasic  \"Carnegie Classification 2005/2010: Basic\"\n",
    "label variable carnegie \"Carnegie Classification 2000\"\n",
    "label variable landgrnt \"Land Grant Institution\"\n",
    "label variable instsize \"Institution size category\"\n",
    "label variable f1systyp \"Multi-institution or multi-campus organization\"\n",
    "label variable f1sysnam \"Name of multi-institution or multi-campus organization\"\n",
    "label variable f1syscod \"Identification number of multi-institution or multi-campus organization\"\n",
    "label variable cbsa     \"Core Based Statistical Area (CBSA)\"\n",
    "label variable cbsatype \"CBSA Type Metropolitan or Micropolitan\"\n",
    "label variable csa      \"Combined Statistical Area (CSA)\"\n",
    "label variable necta    \"New England City and Town Area (NECTA)\"\n",
    "label variable countycd \"Fips County code\"\n",
    "label variable countynm \"County name\"\n",
    "label variable cngdstcd \"State and 114TH Congressional District ID\"\n",
    "label variable longitud \"Longitude location of institution\"\n",
    "label variable latitude \"Latitude location of institution\"\n",
    "label variable dfrcgid  \"Data Feedback Report comparison group created by NCES\"\n",
    "label variable dfrcuscg \"Data Feedback Report - Institution submitted a custom comparison group\"\n",
    "label define label_fips 1 \"Alabama\"\n",
    "label define label_fips 2 \"Alaska\",add\n",
    "label define label_fips 4 \"Arizona\",add\n",
    "label define label_fips 5 \"Arkansas\",add\n",
    "label define label_fips 6 \"California\",add\n",
    "label define label_fips 8 \"Colorado\",add\n",
    "label define label_fips 9 \"Connecticut\",add\n",
    "label define label_fips 10 \"Delaware\",add\n",
    "label define label_fips 11 \"District of Columbia\",add\n",
    "label define label_fips 12 \"Florida\",add\n",
    "label define label_fips 13 \"Georgia\",add\n",
    "label define label_fips 15 \"Hawaii\",add\n",
    "label define label_fips 16 \"Idaho\",add\n",
    "label define label_fips 17 \"Illinois\",add\n",
    "label define label_fips 18 \"Indiana\",add\n",
    "label define label_fips 19 \"Iowa\",add\n",
    "label define label_fips 20 \"Kansas\",add\n",
    "label define label_fips 21 \"Kentucky\",add\n",
    "label define label_fips 22 \"Louisiana\",add\n",
    "label define label_fips 23 \"Maine\",add\n",
    "label define label_fips 24 \"Maryland\",add\n",
    "label define label_fips 25 \"Massachusetts\",add\n",
    "label define label_fips 26 \"Michigan\",add\n",
    "label define label_fips 27 \"Minnesota\",add\n",
    "label define label_fips 28 \"Mississippi\",add\n",
    "label define label_fips 29 \"Missouri\",add\n",
    "label define label_fips 30 \"Montana\",add\n",
    "label define label_fips 31 \"Nebraska\",add\n",
    "label define label_fips 32 \"Nevada\",add\n",
    "label define label_fips 33 \"New Hampshire\",add\n",
    "label define label_fips 34 \"New Jersey\",add\n",
    "label define label_fips 35 \"New Mexico\",add\n",
    "label define label_fips 36 \"New York\",add\n",
    "label define label_fips 37 \"North Carolina\",add\n",
    "label define label_fips 38 \"North Dakota\",add\n",
    "label define label_fips 39 \"Ohio\",add\n",
    "label define label_fips 40 \"Oklahoma\",add\n",
    "label define label_fips 41 \"Oregon\",add\n",
    "label define label_fips 42 \"Pennsylvania\",add\n",
    "label define label_fips 44 \"Rhode Island\",add\n",
    "label define label_fips 45 \"South Carolina\",add\n",
    "label define label_fips 46 \"South Dakota\",add\n",
    "label define label_fips 47 \"Tennessee\",add\n",
    "label define label_fips 48 \"Texas\",add\n",
    "label define label_fips 49 \"Utah\",add\n",
    "label define label_fips 50 \"Vermont\",add\n",
    "label define label_fips 51 \"Virginia\",add\n",
    "label define label_fips 53 \"Washington\",add\n",
    "label define label_fips 54 \"West Virginia\",add\n",
    "label define label_fips 55 \"Wisconsin\",add\n",
    "label define label_fips 56 \"Wyoming\",add\n",
    "label define label_fips 60 \"American Samoa\",add\n",
    "label define label_fips 64 \"Federated States of Micronesia\",add\n",
    "label define label_fips 66 \"Guam\",add\n",
    "label define label_fips 68 \"Marshall Islands\",add\n",
    "label define label_fips 69 \"Northern Marianas\",add\n",
    "label define label_fips 70 \"Palau\",add\n",
    "label define label_fips 72 \"Puerto Rico\",add\n",
    "label define label_fips 78 \"Virgin Islands\",add\n",
    "label values fips label_fips\n",
    "label define label_obereg 0 \"US Service schools\"\n",
    "label define label_obereg 1 \"New England CT ME MA NH RI VT\",add\n",
    "label define label_obereg 2 \"Mid East DE DC MD NJ NY PA\",add\n",
    "label define label_obereg 3 \"Great Lakes IL IN MI OH WI\",add\n",
    "label define label_obereg 4 \"Plains IA KS MN MO NE ND SD\",add\n",
    "label define label_obereg 5 \"Southeast AL AR FL GA KY LA MS NC SC TN VA WV\",add\n",
    "label define label_obereg 6 \"Southwest AZ NM OK TX\",add\n",
    "label define label_obereg 7 \"Rocky Mountains CO ID MT UT WY\",add\n",
    "label define label_obereg 8 \"Far West AK CA HI NV OR WA\",add\n",
    "label define label_obereg 9 \"Outlying areas AS FM GU MH MP PR PW VI\",add\n",
    "label values obereg label_obereg\n",
    "label define label_opeflag 1 \"Participates in Title IV federal financial aid programs\"\n",
    "label define label_opeflag 2 \"Branch campus of a main campus that participates in Title IV\",add\n",
    "label define label_opeflag 3 \"Deferment only - limited participation\",add\n",
    "label define label_opeflag 4 \"New participants (became eligible during winter collection)\",add\n",
    "label define label_opeflag 5 \"Not currently participating in Title IV, has an OPE ID number\",add\n",
    "label define label_opeflag 6 \"Not currently participating in Title IV, does not have OPE ID number\",add\n",
    "label define label_opeflag 7 \"Stopped participating during the survey year\",add\n",
    "label values opeflag label_opeflag\n",
    "label define label_sector 0 \"Administrative Unit\"\n",
    "label define label_sector 1 \"Public, 4-year or above\",add\n",
    "label define label_sector 2 \"Private not-for-profit, 4-year or above\",add\n",
    "label define label_sector 3 \"Private for-profit, 4-year or above\",add\n",
    "label define label_sector 4 \"Public, 2-year\",add\n",
    "label define label_sector 5 \"Private not-for-profit, 2-year\",add\n",
    "label define label_sector 6 \"Private for-profit, 2-year\",add\n",
    "label define label_sector 7 \"Public, less-than 2-year\",add\n",
    "label define label_sector 8 \"Private not-for-profit, less-than 2-year\",add\n",
    "label define label_sector 9 \"Private for-profit, less-than 2-year\",add\n",
    "label define label_sector 99 \"Sector unknown (not active)\",add\n",
    "label values sector label_sector\n",
    "label define label_iclevel 1 \"Four or more years\"\n",
    "label define label_iclevel 2 \"At least 2 but less than 4 years\",add\n",
    "label define label_iclevel 3 \"Less than 2 years (below associate)\",add\n",
    "label define label_iclevel -3 \"{Not available}\",add\n",
    "label values iclevel label_iclevel\n",
    "label define label_control 1 \"Public\"\n",
    "label define label_control 2 \"Private not-for-profit\",add\n",
    "label define label_control 3 \"Private for-profit\",add\n",
    "label define label_control -3 \"{Not available}\",add\n",
    "label values control label_control\n",
    "}\n",
    "\n",
    "*Save dataset for Figure 1\n",
    "save hdefa_dist20122019, replace"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Stata",
   "language": "stata",
   "name": "stata"
  },
  "language_info": {
   "codemirror_mode": "stata",
   "file_extension": ".do",
   "mimetype": "text/x-stata",
   "name": "stata",
   "version": "15.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
